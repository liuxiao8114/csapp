#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
# Loop header
  xorq %rax,%rax		  # count = 0
  irmovq $8, %rbx     # set k = 8

  rrmovq %rdx, %rbp   # i = len
  subq %rbx, %rdx     # len -= k
  jge Unroll8         # if i >= 0, goto Unroll8

Unroll2Prev:
  irmovq $2, %rbx     # set k = 2
  subq %rbx, %rbp     # i -= k
  jge Unroll2         # if i >= 0, goto Unroll2
  iaddq $1, %rbp
  jl Done

One:
  mrmovq (%rdi), %r10	# read val from src...
  irmovq $1, %rcx
  addq %rax, %rcx     # count++ here for cmov, otherwise data dependency happened here.
  rmmovq %r10, (%rsi)	# ...and store it to dst
  andq %r10, %r10			# val <= 0?
  cmovg %rcx, %rax
  ret

Unroll8:
	mrmovq   (%rdi), %r8	# read val from src...
	mrmovq  8(%rdi), %r9	# read val from src...
	mrmovq 16(%rdi), %r10	# read val from src...
	mrmovq 24(%rdi), %r11	# read val from src...
	mrmovq 32(%rdi), %r12	# read val from src...
	mrmovq 40(%rdi), %r13	# read val from src...
	mrmovq 48(%rdi), %r14	# read val from src...
	mrmovq 56(%rdi), %rcx	# read val from src...
	rmmovq %r8,   (%rsi)	# ...and store it to dst
	rmmovq %r9,  8(%rsi)	# ...and store it to dst
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	rmmovq %r11, 24(%rsi)	# ...and store it to dst
	rmmovq %r12, 32(%rsi)	# ...and store it to dst
	rmmovq %r13, 40(%rsi)	# ...and store it to dst
	rmmovq %r14, 48(%rsi)	# ...and store it to dst
	rmmovq %rcx, 56(%rsi)	# ...and store it to dst

	andq %r8, %r8		  	# val <= 0?
  jle Val2
  iaddq $1, %rax			# count++
Val2:
  andq %r9, %r9			  # val <= 0?
  jle Val3
  iaddq $1, %rax			# count++
Val3:
  andq %r10, %r10			# val <= 0?
  jle Val4
  iaddq $1, %rax			# count++
Val4:
  andq %r11, %r11			# val <= 0?
  jle Val5
  iaddq $1, %rax			# count++
Val5:
  andq %r12, %r12			# val <= 0?
  jle Val6
  iaddq $1, %rax			# count++
Val6:
  andq %r13, %r13			# val <= 0?
  jle Val7
  iaddq $1, %rax			# count++
Val7:
  andq %r14, %r14			# val <= 0?
  jle Val8
  iaddq $1, %rax			# count++
Val8:
  andq %rcx, %rcx			# val <= 0?
  jle LoopJudgement8
  iaddq $1, %rax			# count++

LoopJudgement8:
  iaddq $64, %rdi		  # src += 8
  iaddq $64, %rsi		  # dst += 8
	subq %rbx, %rdx			# i -= k
	jge Unroll8

  addq %rbx, %rdx     # i = k + i
  rrmovq %rdx, %rbp
  jmp Unroll2Prev

Unroll2:
	mrmovq   (%rdi), %r8	# read val from src...
	mrmovq  8(%rdi), %r9	# read val from src...
	rmmovq %r8,    (%rsi)	# ...and store it to dst
	rmmovq %r9,   8(%rsi)	# ...and store it to dst

  andq %r8, %r8			# val <= 0?
  jle U4Val2
  iaddq $1, %rax			# count++
U4Val2:
  andq %r9, %r9			# val <= 0?
  jle LoopJudgement2
  iaddq $1, %rax			# count++
LoopJudgement2:
  iaddq $16, %rdi	  	# src += 2
  iaddq $16, %rsi	  	# dst += 2
	subq %rbx, %rbp			# i -= k
	jge Unroll2
  addq %rbp, %rbx     # k = k + i
  jg One
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
